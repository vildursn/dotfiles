# ==============================================================================
# Uncomment to profile shell with zprof.
# zmodload zsh/zprof
# ------------------------------------------------------------------------------


##
# Powerlevel10k Instant Promptâ„¢
# -----------------------------

# Begin initializing direnv, if installed. Doing it here is necessary to ensure
# that direnv is initialized before Instant Prompt does its thing.
(( ${+commands[direnv]} )) && emulate zsh -c "$(direnv export zsh)"

# Initialize Instant Prompt.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Finalize direnv initialization.
(( ${+commands[direnv]} )) && emulate zsh -c "$(direnv hook zsh)"


##
# OH MY ZSH!
# ----------

export ZSH_DISABLE_COMPFIX="true"
export ZSH="$HOME/.oh-my-zsh"
export ZSH_CUSTOM="$HOME/.local/share/oh-my-zsh"

# Disable auto-update if no .git directory is found.
if [[ ! -d "$ZSH/.git" ]]; then
  DISABLE_AUTO_UPDATE="true"
fi

CASE_SENSITIVE="true"
DISABLE_AUTO_TITLE="false"
ZSH_THEME="powerlevel10k/powerlevel10k"

# Enable brew completions to be automatically picked up.
# NOTE: We need to set FPATH before bootstrapping Oh My Zsh.
if [[ -d "/opt/homebrew" ]]; then
  FPATH="/opt/homebrew/share/zsh/site-functions:${FPATH}"
fi

# Enable user-defined completions to be automatically picked up.
FPATH="$HOME/.local/share/zsh/completions:${FPATH}"

plugins=(
  evalcache
  git
  gitfast
  jump
  norce
)

zstyle ':completion:*:*:docker:*' option-stacking yes
zstyle ':completion:*:*:docker-*:*' option-stacking yes

source "$ZSH/oh-my-zsh.sh"


##
# Aliases
# -------

alias less="less -R"
alias printenv="printenv | LC_ALL=C sort"
alias tree="tree -CA"

# Shorthands
alias cm="chezmoi"
alias d="docker"
alias dc="docker-compose"
alias k="kubectl"

# jump (oh-my-zsh plugin)
alias J="jump"
alias M="mark"

# safe-rm: prevents potential disasters...
if [[ -x "$(command -v safe-rm)" ]]; then alias rm="safe-rm"; fi

# exa -> ls
if [[ -x "$(command -v exa)" ]]; then
  exa_base="exa --group-directories-first"
  exa_long="${exa_base} --long --git --classify"

  alias ls="${exa_base}"
  alias ll="${exa_long}"
  alias la="${exa_long} --all"
  alias lx="${exa_long} --all --extended"

  alias lS="${exa_base} --oneline"
  alias lT="${exa_base} --tree --level=2"

  # Remove leftover, unwanted ls aliases...
  ls_aliases=( $(alias | grep "ls " | cut -d= -f1) )
  for leftover in "${ls_aliases[@]}"; do
    unalias "${leftover}"
  done
fi


##
# Bootstrappers
# -------------

# Initialize direnv if installed.
# (( ${+commands[direnv]} )) && _evalcache direnv hook zsh

# Initialize pyenv if installed.
(( ${+commands[pyenv]} )) && _evalcache pyenv init - --no-rehash

# Setup hub alias if hub is installed. (git -> hub)
(( ${+commands[hub]} )) && _evalcache hub alias -s


##
# Miscellaneous
# -------------

# Ensure hostname is displayed as lowercase.
export HOST="${HOST:l}"
export HOSTNAME="${HOST}"

# Suppress annoying Python warnings.
export PYTHONWARNINGS="ignore:deprecation:Warning"


##
# VS Code integrated terminal hacks
# ---------------------------------
# TODO: Consider making this into an omz plugin.
if [[ "$TERM_PROGRAM" == "vscode" ]]; then
  # This is a silly hack to ensure direnv environments are initialized when a
  # new integrated terminal is opened. The underlying problem is that when an
  # integrated terminal is initialized, it is started in the workspace root.
  # However, direnv requires actively navigating to that directory to trigger
  # the whole environment initialization procedure.
  if [[ -f "$PWD/.envrc" ]]; then
    pushd ..  >/dev/null 2>&1 && popd >/dev/null 2>&1 || true
  fi
fi


##
# Powerlevel10k theme customization
# ---------------------------------
# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh


# ------------------------------------------------------------------------------
# Uncomment to profile shell startup time, and print the results to stdout.
# zprof
# ==============================================================================
